Entity {
    Name: "Unknown",
    Strength: 30,
    Dexterity: 30,
    Willpower: 30,
    Health: (this.Strength + 2 * this.Dexterity) / 10,
    Equipment: []
};

trait Strong for Entity {
    Strength: base.Strength + 10
};

fun fight(e1: Entity, e2: Entity) Entity {
    print(e1.Name ++ " has " ++ getString(e1.Health) ++ " health\n");
    print(e2.Name ++ " has " ++ getString(e2.Health) ++ " health\n");
    set r1 = roll(1d100) - e1.Strength / 10;
    set r2 = roll(1d100) - e2.Strength / 10;
    set result = r1 - r2;
    set loser = if (result > 0)
        then e2
        else e1;
    set winner = if (result > 0)
        then e1
        else e2;
    set damage = winner.Strength / 10;
    set loser.Health = loser.Health - damage;
    print(loser.Name ++ " lost " ++ getString(damage) ++ " health.\n");
    if (loser.Health <= 0)
        then {
            print(loser.Name ++ " has died!\n");
            print(winner.Name ++ " has won!\n");
            winner;
        }
        else {
            print("Fight continues!\n");
            fight(winner, loser);
        };
};

set fighter = Entity with Strong;
set fighter.Name = "Marcus";
set bandit = Entity;
set bandit.Name = "Otto";

fight(fighter, bandit);